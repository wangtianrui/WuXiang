apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "1.0.4"

android {
    compileSdkVersion rootProject.ext.android["compileSdkVersion"]
    buildToolsVersion rootProject.ext.android["buildToolsVersion"]

    defaultConfig {
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]


    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile rootProject.ext.dependencies["appcompat-v7"]
    testCompile 'junit:junit:4.12'
    //RxAndroid
    compile rootProject.ext.dependencies["rxandroid2"]
    //RxJava2
    compile rootProject.ext.dependencies["rxjava2"]
    //RxPermission
    compile rootProject.ext.dependencies["RxPermissions"]
    //Retrofit2的RxJava适配
    compile rootProject.ext.dependencies["retrofit2-adapter-rxjava"]
    //Retrofit2
    compile rootProject.ext.dependencies["retrofit2"]
    //RxJava2Gson适配
    compile rootProject.ext.dependencies["retrofit2-converter-gson"]
    //网络日志拦截
    compile rootProject.ext.dependencies["okhttp3-intercepter"]
    //X5浏览器内核
    compile files('libs/tbs_sdk_thirdapp_v3.1.0.1034_43100_sharewithdownload_obfs_20170301_182143.jar')
    //ButterKnife
    compile rootProject.ext.dependencies["butterknife"]


}



def siteUrl = 'https://github.com/Cicinnus0407/RRMLib'      // 项目的主页
def gitUrl = 'https://github.com/Cicinnus0407/RRMLib.git'   // Git仓库的url
group = "com.cicinnus.RRMLib"                           // Maven Group ID for the artifact，
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name 'RxJava2+Retrofit+MVP封装库'    //项目描述
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'cicinnus'        //填写的一些基本信息
                        name 'cicinnus'
                        email 'cicinnus0407@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding = "UTF-8" //添加编码方式，才不会报编码错误
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}




Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
//配置jCenter
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "RRMLib"    //发布到JCenter上的项目名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
        version {
            name = '1.0.4'
            desc = '添加工具类'
            released  = new Date()
            vcsTag = 'v1.0.4'
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
}
